7 '
8 'configure system preferences file to set locale and load default code page
9 '
10 ON ERROR CONT 
11 MKDIR "/SYSTEM"
12 MKDIR "/SYSTEM/PREFEREN.CES"
13 ON ERROR STOP 
14 CLEAR 32767
15 OUT $7FFD,%11111
16 BLOAD "/SYSTEM/FONTS/0437-IBM.CP",$F800
17 OUT $7FFD,%11000: CLEAR 65535
18 'arrays for language settings
19 '
20 DIM N$(45, 24): DIM L$(45, 8) : REM Name, Language
21 DIM C$(45, 9) : DIM K$(45, 8) : REM Code page, Keyboard
22 DIM P$(20, 9) : DIM D$(20, 32): REM code Page, Description
23 DIM F$(9, 8) : DIM A$(9, 16) : REM File name, keyboard Arrangement
27 '
28 'load data into arrays
29 '
30 RESTORE 1000: REM start of data
31 FOR i=1 TO 45
32 READ N$(i),L$(i),C$(i),K$(i)
33 NEXT i
51 FOR i=1 TO 20
52 READ P$(i),D$(i)
53 NEXT i
71 FOR i=1 TO 9
72 READ F$(i),A$(i)
73 NEXT i
99 COLOR 4,7
100 PRINT "============== Configuration Tool for SE Basic IV 4.20b9 Cordelia =============="''
110 PRINT "SELECT LANGUAGE"''
120 FOR I=1 TO 45 STEP 3
130 PRINT TAB (1);" " AND I<10;I;". ";N$(I);TAB (30);" " AND I+1<10;I+1;". ";N$(I+1);TAB (60);" " AND I+2<10;I+2;". ";N$(I+2);
140 NEXT I
150 INPUT L
160 IF L<1 OR L>45 THEN GOTO 150
180 CLS 
200 PRINT "============== Configuration Tool for SE Basic IV 4.20b9 Cordelia =============="''
205 PRINT "LANGUAGE: ";N$(L)''
210 PRINT "SELECT CODE PAGE"''"Recommended: ";
220 FOR I=1 TO 20
230 IF C$(L)=P$(I) THEN PRINT D$(I);''
240 NEXT I
250 FOR I=1 TO 20 STEP 2
260 PRINT TAB (1);" " AND I<10;I;". ";D$(I);TAB (40);" " AND I+1<10;I+1;". ";D$(I+1)
270 NEXT I
280 INPUT C
290 IF C<1 OR C>20 THEN GOTO 280
295 CLS 
300 PRINT "============== Configuration Tool for SE Basic IV 4.20b9 Cordelia =============="''
305 PRINT "LANGUAGE: ";N$(L)''
310 PRINT "CODE PAGE: ";D$(C)''
320 PRINT "SELECT KEYBOARD LAYOUT"''"Recommended: ";
330 FOR I=1 TO 9
340 IF K$(L)=F$(I) THEN PRINT A$(I);''
350 NEXT I
360 FOR I=1 TO 9
370 PRINT " ";I;". ";A$(I)
380 NEXT I
390 INPUT K
395 IF K<1 OR K>9 THEN GOTO 390
396 CLS 
400 PRINT "============== Configuration Tool for SE Basic IV 4.20b9 Cordelia =============="''
401 PRINT "LANGUAGE: ";N$(L)''
402 PRINT "CODE PAGE: ";D$(C)''
403 PRINT "KEYBOARD LAYOUT: ";A$(K)''
404 PRINT "ENABLE SCAN DOUBLER? (recommended for VGA)"''
405 PRINT " 1. Enable"''" 2. Disable"
406 INPUT S
407 IF S<1 OR S>2 THEN GOTO 406
408 IF S=1 THEN SC$="on": ELSE SC$="off"
409 LOCATE 9,1: PRINT "SCAN DOUBLER: ";"Enabled" AND S=1;"Disabled" AND S=2;STRING$(22,32)''
410 PRINT "EXIT      "''" 1. Exit without saving"''" 2. Write changes and exit"
411 INPUT X
480 CR$=chr$ 10+chr$ 10
497 '
498 'write out preferences file
499 '
500 OPEN #3,"O","/SYSTEM/PREFEREN.CES/SYSTEM.PRF"
510 PRINT #3;"#### Preferences file generated by LOCALE app";CR$;
520 PRINT #3;"### language (including code page if required)";CR$;
530 PRINT #3;"ln=";
531 I=1
532 I$=L$(L,I)
533 IF i$<>" " THEN PRINT #3;I$;: I=I+1: GOTO 532
540 PRINT #3;CR$;"### code page (8-bit system font)";CR$;
550 PRINT #3;"cp=";
551 I=1
552 I$=P$(C,I)
553 IF i$<>" " THEN PRINT #3;I$;: I=I+1: GOTO 552
560 PRINT #3;CR$;"### keyboard (generated by the keymap.c)";CR$;
570 PRINT #3;"kb=";
571 I=1
572 I$=F$(K,I)
573 IF i$<>" " THEN PRINT #3;I$;: I=I+1: GOTO 572
580 PRINT #3;CR$;"### scandoubler setting (default depends on hardware, on recommended for VGA)";CR$;
590 PRINT #3;"sc=";SC$;CR$;
600 CLOSE #3
610 CALL 0: REM hard reset to change settings
997 '
998 'name, language, code page, keyboard
999 '
1001 DATA "Albanian",                 "SQ-1250",  "1250-EUC", "US"
1002 DATA "Basque",                   "EU",       "0437-IBM", "US"
1003 DATA "Bulgarian",                "BG-1251",  "1251-CYR", "US"
1004 DATA "Bosnian (Latin)",          "BS-1250",  "1250-EUC", "US"
1005 DATA "Catalan",                  "CA-1252",  "1252-EUW", "US"
1006 DATA "Chinese (Pinyin)",         "ZH-PY",    "1252-PY",  "US"
1007 DATA "Croatian",                 "HR-1252",  "1252-EUW", "US"
1008 DATA "Czech",                    "CS-1250",  "1250-EUC", "US"
1009 DATA "Danish",                   "DA-1252",  "1252-EUW", "US"
1010 DATA "Dutch",                    "NL",       "0437-IBM", "US"
1011 DATA "English",                  "EN",       "0437-IBM", "CHLOE"
1012 DATA "Esperanto",                "EO-IR109", "IR-109",   "US"
1013 DATA "Estonian",                 "ET-1252",  "1252-EUW", "US"
1014 DATA "Finnish",                  "FI-1252",  "1252-EUW", "US"
1015 DATA "French",                   "FR-1252",  "1252-EUW", "AZERTY"
1016 DATA "German",                   "DE-1252",  "1252-EUW", "QWERTZ"
1017 DATA "Greek",                    "EL-1253",  "1253-GRE", "US"
1018 DATA "Hungarian",                "HU-1252",  "1252-EUW", "US"
1019 DATA "Icelandic",                "IS-1252",  "1252-EUW", "US"
1020 DATA "Irish",                    "GA-1252",  "1252-EUW", "US"
1021 DATA "Indonesian",               "ID",       "0437-IBM", "US"
1022 DATA "Italian",                  "IT",       "0437-IBM", "US"
1023 DATA "Japanese",                 "JA-0932",  "0932-JAP", "US"
1024 DATA "Latvian",                  "LV-1257",  "1257-BAL", "US"
1025 DATA "Lithuanian",               "LT-1257",  "1257-BAL", "US"
1026 DATA "Macedonian",               "MK-1251",  "1251-CYR", "US"
1027 DATA "Maltese",                  "MT-IR109", "IR-109",   "US"
1028 DATA "Norwegian (Bokmal)",       "NO-1252",  "1252-EUW", "US"
1029 DATA "Polish",                   "PL-1250",  "1250-EUC", "US"
1030 DATA "Portuguese ",              "PT-1252",  "1252-EUW", "US"
1031 DATA "Portuguese (Brazil)",      "PTBR1252", "1252-EUW", "BR"
1032 DATA "Romanian",                 "RO-IR226", "IR-226",   "US"
1033 DATA "Russian",                  "RU-1251",  "1251-CYR", "US"
1034 DATA "Russian (Jcuken)",         "RU-KOI8R", "KOI8-R",   "JCUKEN"
1035 DATA "Serbian (Cyrillic)",       "SR-1251",  "1251-CYR", "US"
1036 DATA "Serbian (Latin)",          "SR-1250",  "1250-EUC", "US"
1037 DATA "Slovak",                   "SK-1250",  "1250-EUC", "US"
1038 DATA "Slovene",                  "SL-1250",  "1250-EUC", "US"
1039 DATA "Spanish",                  "ES-1252",  "1252-EUW", "ES"
1040 DATA "Spanish (Latin American)", "ESLA1252", "1252-EUW", "ES"
1041 DATA "Swahili",                  "SW",       "0437-IBM", "US"
1042 DATA "Swedish",                  "SV-1252",  "1252-EUW", "US"
1043 DATA "Turkish",                  "TR-1254",  "1254-TUR", "US"
1044 DATA "Ukrainian",                "UK-1251",  "1251-CYR", "US"
1045 DATA "Welsh",                    "CY-1252",  "1252-EUW", "US"
1997 '
1998 'code, name
1999 '
2001 DATA "1252-EUW", "Latin 1   (Western Europe)"
2002 DATA "1250-EUC", "Latin 2   (Central Europe)"
2003 DATA "IR-109",   "Latin 3   (South Europe)"
2004 DATA "IR-226",   "Latin 10  (South-Eastern Europe)"
2005 DATA "1257-BAL", "Baltic"
2006 DATA "1252-PY",  "Chinese   (Pinyin)"
2007 DATA "1251-CYR", "Cyrillic"
2008 DATA "KOI8-R",   "Cyrillic  (Jcuken)"
2009 DATA "1253-GRE", "Greek"
2010 DATA "1255-HEB", "Hebrew"
2011 DATA "0932-JAP", "Japanese  (MSX 2+)"
2012 DATA "1254-TUR", "Turkish"
2013 DATA "APPLE-II", "Apple II"
2014 DATA "ATASCII",  "Atari     (8-bit)"
2015 DATA "ATARI-ST", "Atari ST"
2016 DATA "PETSCII",  "Commodore (8-bit)"
2017 DATA "0437-IBM", "IBM PC"
2018 DATA "MACROMAN", "Mac       (Roman)"
2019 DATA "NEXT",     "NeXT"
2020 DATA "RISC-OS",  "RISC OS"
2997 '
2998 'keyboard, name
2999 '
3001 DATA "CHLOE",   "Chloe (Terminal)"
3002 DATA "COLEMAK", "Colemak (US)"
3003 DATA "DVORAK",  "Dvorak (US)"
3004 DATA "US",      "English (US)"
3005 DATA "AZERTY",  "French (Azerty)"
3006 DATA "QWERTZ",  "German (Qwertz)"
3007 DATA "BR",      "Portuguese (Brazil)"
3008 DATA "JCUKEN",  "Russian (Jcuken)"
3009 DATA "ES",      "Spanish"
